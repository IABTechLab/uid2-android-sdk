import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.dokka'
    id 'com.vanniktech.maven.publish'
}

apply from:  rootProject.file("$rootDir/common.gradle")

android {
    namespace 'com.uid2'
    defaultConfig {
        minSdk 19
    }

    sourceSets {
        main {
            assets {
                srcDirs 'src/main/assets'
            }
        }

        test {
            resources {
                srcDirs 'src/test/assets'
            }
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.13.2'

    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
    testImplementation 'org.mockito:mockito-inline:4.8.0'

    testImplementation 'org.json:json:20180813'

    dokkaPlugin 'org.jetbrains.dokka:android-documentation-plugin:1.8.10'
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Dokka Configuration

dokkaHtml {
    outputDirectory.set(file("build/documentation/html"))
}

dokkaJavadoc {
    outputDirectory.set(file("build/documentation/javadoc"))
}

// Task to generate a Jar file from the HTML documentation.
tasks.register('dokkaHtmlJar', Jar.class) {
    dependsOn(dokkaHtml)
    from(dokkaHtml)
    archiveClassifier.set("html-docs")
}

// Task to generate a Jar file from the Javadoc documentation.
tasks.register('dokkaJavadocJar', Jar.class) {
    dependsOn(dokkaJavadoc)
    from(dokkaJavadoc)
    archiveClassifier.set("javadoc")
}

////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Maven Publishing

mavenPublishing {
    publishToMavenCentral(SonatypeHost.S01, true)
    signAllPublications()

    coordinates(project.group, "uid2-android-sdk", project.version)

    pom {
        name = project.name
        description = 'A framework for integration UID2 into Android applications.'
        url = 'https://github.com/IABTechLab/uid2-android-sdk'
        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }
        developers {
            developer {
                id = 'IABTechLab'
                name = 'IAB Tech Lab'
                url = 'https://iabtechlab.com/'
            }
        }
        scm {
            url = 'https://github.com/IABTechLab/uid2-android-sdk'
            connection = 'scm:git:git@github.com:IABTechLab/uid2-android-sdk.git'
            developerConnection = 'scm:git:ssh://git@github.com:IABTechLab/uid2-android-sdk.git'
        }
    }
}
